plugins {
    id("com.android.application")
    id("org.jetbrains.kotlin.android")
    id("com.google.devtools.ksp")
}

android {
    compileSdk = 30

    defaultConfig {
        applicationId = "me.tatarka.inject.android.test"
        minSdk = 21
        targetSdk = 30
        versionCode = 1
        versionName = "1.0"
        testInstrumentationRunner = "androidx.test.runner.AndroidJUnitRunner"
    }

    sourceSets {
        // allows android studio to see ksp generated code
        debug.java {
            srcDir("build/generated/ksp/debug/kotlin")
        }
        release.java {
            srcDir("build/generated/ksp/release/kotlin")
        }
        testDebug.java {
            srcDir("build/generated/ksp/debugUnitTest/kotlin")
        }
        androidTestDebug.java {
            srcDir("build/generated/ksp/debugAndroidTest/kotlin")
        }
        // share code between unit and android tests
        test.java {
            srcDir("src/commonTest/java")
        }
        androidTest.java {
            srcDir("src/commonTest/java")
        }
    }
}

dependencies {
    implementation(project(":android:activity"))
    implementation(project(":android:fragment"))

    ksp(project(":kotlin-inject-compiler:ksp"))
    ksp(project(":android:fragment-factory-compiler"))

    androidTestImplementation("org.jetbrains.kotlin:kotlin-test")
    androidTestImplementation("junit:junit:4.13.2")
    androidTestImplementation("androidx.test:core-ktx:1.3.0")
    debugImplementation("androidx.fragment:fragment-testing:1.3.3")
    androidTestImplementation("androidx.test.ext:junit:1.1.2")
    androidTestImplementation("androidx.test:runner:1.3.0")
    androidTestImplementation("com.willowtreeapps.assertk:assertk-jvm:0.22")
}

sourceCompatibility = 1.8
targetCompatibility = 1.8

tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).configureEach {
    kotlinOptions.jvmTarget = "1.8"
}